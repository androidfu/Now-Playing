apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'hugo'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

// Look for newer versions of our dependencies
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        jcenter()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.14.7'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    lintOptions {
        abortOnError true
        lintConfig file("${project.rootDir}/config/lint/lint.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("$project.rootDir/reports/lint.html")
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 3
        versionName "1.2"
        resConfigs "en"
        resConfigs "nodpi", "hdpi", "xhdpi",
                "xxhdpi", "xxxhdpi"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles( \
                    getDefaultProguardFile('proguard-android.txt'),         \
                            'proguard-rules.pro',         \
                            'proguard-otto.pro',         \
                            'proguard-okhttp.pro',         \
                            'proguard-crashlytics.pro',         \
                            'proguard-butterknife.pro',         \
                            'proguard-google-analytics.pro',         \
                            'proguard-gson.pro',         \
                            'proguard-picasso.pro',         \
                            'proguard-retrofit.pro',         \
                            'proguard-ormlite.pro',         \
                            'proguard-nowplaying.pro',         \
                            'proguard-parceler.pro',        \
                            'proguard-apache-commons-logging.pro',        \
                            'proguard-google-play-services.pro'         \
            )
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles( \
                    getDefaultProguardFile('proguard-android.txt'),         \
                            'proguard-rules.pro',         \
                            'proguard-otto.pro',         \
                            'proguard-okhttp.pro',         \
                            'proguard-crashlytics.pro',         \
                            'proguard-butterknife.pro',         \
                            'proguard-google-analytics.pro',         \
                            'proguard-gson.pro',         \
                            'proguard-picasso.pro',         \
                            'proguard-retrofit.pro',         \
                            'proguard-ormlite.pro',         \
                            'proguard-nowplaying.pro',         \
                            'proguard-parceler.pro',        \
                            'proguard-apache-commons-logging.pro',        \
                            'proguard-google-play-services.pro'         \
            )
        }
    }

    // Removes this warning:
    //      WARNING: Dependency commons-logging:commons-logging:1.1.1 is ignored for debug as it may be conflicting with the internal version provided by Android.
    //      In case of problem, please repackage it with jarjar to change the class packages
    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.picasso:picasso:2.3.3'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile 'org.parceler:parceler-api:0.2.10'
    provided 'org.parceler:parceler:0.2.10'
    compile 'com.crashlytics.android:crashlytics:1.1.13'
    compile 'com.felipecsl.quickreturn:library:1.5.1'
}

// Usage:
//      ./gradlew app:findbugs
//      open ./reports/findbugs.html
apply plugin: 'findbugs'

task findbugs(type: FindBugs, dependsOn: assembleDebug) {
    excludeFilter file("${project.rootDir}/config/findbugs/exclude.xml")
    classes = fileTree('build/intermediates/classes/debug/')
    source = fileTree('src/main/java/')
    classpath = files()
    effort = 'max'
    reports {

        xml.enabled = false
        xml {
            destination "$project.rootDir/reports/findbugs.xml"
        }

        html.enabled = true
        html {
            destination "$project.rootDir/reports/findbugs.html"
        }

    }
}

// Usage:
//      ./gradlew app:pmd
//      open ./reports/pmd.html
apply plugin: 'pmd'

task pmd(type: Pmd, dependsOn: assembleDebug) {
    ruleSetFiles = files("${project.rootDir}/config/pmd/pmd-ruleset.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/jp/co/cyberagent/android/gpuimage/*'
    exclude '**/*Tests.java'
    exclude '**/*TestCase.java'

    reports {

        xml.enabled = false
        xml {
            destination "$project.rootDir/reports/pmd.xml"
        }

        html.enabled = true
        html {
            destination "$project.rootDir/reports/pmd.html"
        }
    }
}

// Usage:
//      ./gradlew app:checkstyle
//      open reports/checkstyle.xml
apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()

    reports {
        xml.enabled = true
        xml {
            destination "$project.rootDir/reports/checkstyle.xml"
        }
    }
}